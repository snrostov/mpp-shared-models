plugins {
    id 'kotlin-multiplatform'
    id 'kotlinx-serialization'
}

repositories {
    jcenter()
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "http://dl.bintray.com/kotlinx/kotlinx" }
    maven { url "http://dl.bintray.com/kotlin/kotlin-js-wrappers" }
    maven { url "https://kotlin.bintray.com/kotlin-dev" }
    mavenCentral()
}

def ktor_version = '1.0.0'
def logback_version = '1.2.3'

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js') {
            configure([compilations.main, compilations.test]) {
                tasks.getByName(compileKotlinTaskName).kotlinOptions {
                    sourceMap = true
                    moduleKind = "commonjs"
                    metaInfo = true
                    sourceMapEmbedSources = "always"
                }
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.9.0"
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.0"
                implementation 'org.joda:joda-money:1.0.1'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:0.9.0"
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }
    }
}

def jsCompilations = kotlin.targets.js.compilations
def npmSharedPackage = "../npm/packages/shared"

task makeNpmHappy(dependsOn: [jsMainClasses]) {
    doLast {
        // for running tests
        copy {
            from jsCompilations.main.output
            jsCompilations.test.runtimeDependencyFiles.each {
                if (it.exists() && !it.isDirectory()) {
                    from zipTree(it.absolutePath)
                }
            }
            include '*.js'
            into 'node_modules'
        }

        copy {
            jsCompilations.main.runtimeDependencyFiles.each {
                if (it.exists() && !it.isDirectory()) {
                    from zipTree(it.absolutePath)
                }
            }
            include '*.js'
            into "$npmSharedPackage/node_modules"
        }

        copy {
            from "package.json"
            from jsCompilations.main.output
            into npmSharedPackage
        }
    }
}

assemble.dependsOn(makeNpmHappy)